---
title: Именованные параметры
---

{% include menu.html %}

В отличие от [позиционных параметров](../positional-parameters), _именованные_ параметры указываются по их именам.

Следующая функция принимает два параметра, называемых `$from` и `$to`.

```raku
sub distance(:$from, :$to) { $from - $to }
```

Теперь, чтобы вызвать функцию, нужно указать имена аргументов:

```raku
say distance(from => 30, to => 10); # 20
```

Ошибка возникает, если передать аргументы, как будто они позиционные. Например, вызов `distance(30, 10)` приведет к ошибке:

    Слишком много позиционных аргументов; ожидалось 0 аргументов, но получено 2
        в подпрограмме distance на строке 1 в t.raku
        в блоке <unit> на строке 2 в t.raku

Хорошая новость в том, что именованные аргументы можно перечислять в любом порядке. Следующие два вызова абсолютно эквивалентны:

```raku
say distance(from => 30, to => 10); # 20

say distance(to => 10, from => 30); # 20
```

## Передача переменных

Когда значение, которое вы хотите передать функции, хранится в переменной, имя которой совпадает с именем параметра, вы можете использовать специальный синтаксис, который уменьшает количество вводимых символов:

```raku
my $from = 30;
my $to = 10;
say distance(:$from, :$to); # 20
```

Это похоже на более многословный вызов:

```raku
say distance(from => $from, to => $to); # 20
```

Снова, порядок здесь не важен:

```raku
say distance(:$to, :$from); # 20
```

Если имя переменной отличается от имени параметра, используйте один из способов передачи пары:

```raku
my $a = 20;
my $b = 10;

say distance(from => $a, to => $b);

# или:

say distance(:from($a), :to($b));
```

{% include nav.html %}